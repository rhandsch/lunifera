grammar org.lunifera.metamodel.dsl.behavior.stories.en.StoryDsl hidden(WS, NEWLINE, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate storyDsl "http://www.lunifera.org/metamodel/dsl/behavior/stories/en/StoryDsl"

Story:
	description+=Description?
	meta=Meta?
	narrative=Narrative?
	scenarios+=Scenario*;

Narrative:
	'Narrative:'
	((inOrderTo=InOrderTo?) &
	(asA=AsA?) &
	iWantTo=IWantTo);

Scenario:
	{Scenario}
	'Scenario:'
	name=Description
	(meta=Meta)?
	(givenStories=GivenStories)?
	(steps+=AbstractStep)*
	(examples=Examples)?;

GivenStories:
	'GivenStories:'
	resources+=StoryPath+;

StoryPath:
	path=PATH ','?;

Meta:
	{Meta}
	'Meta:'
	metaElements+=MetaElement (metaItems+=MetaElement)*	
;

MetaElement:
	'@' name=WORD value=WORD
;

AbstractStep:
	(GivenStep | WhenStep | ThenStep)
	(=> and=AndStep)?;

GivenStep:
	{GivenStep}
	'Given' sentence=StepDescription NEWLINE*
	table=Table?;

WhenStep:
	{WhenStep}
	'When' sentence=StepDescription NEWLINE*
	table=Table?;

ThenStep:
	{ThenStep}
	'Then' sentence=StepDescription NEWLINE*
	table=Table?;

AndStep:
	{AndStep}
	'And' sentence=StepDescription NEWLINE*
	table=Table?;

InOrderTo:
	'In order to' content=NarrativeElementContent;

AsA:
	'As a' content=NarrativeElementContent;

IWantTo:
	'I want to' content=NarrativeElementContent;

Examples:
	'Examples:'
	table=Table;

Table:
	rows+=TABLE_ROW+ NEWLINE*;

Description hidden():
	Content NEWLINE+;

StepDescription hidden():
	(WS | STRING | WORD | INT | ANY | PLACEHOLDER)+;

NarrativeElementContent hidden():
	(STRING | WORD | INT | ANY | PLACEHOLDER) (STRING | WORD | INT | ANY | PLACEHOLDER )*;

Content:
	(WS | STRING | WORD | INT | ANY | PLACEHOLDER) (WS | STRING | WORD | INT | ANY | PLACEHOLDER )*;


terminal PLACEHOLDER:
	'<' ('a'..'z' | 'A'..'Z') !('>' | '\n' | '\r')* '>';

terminal TABLE_ROW:
	'|' (!('|' | '\n' | '\r')* '|')+ WS* NEWLINE;

terminal WORD:
	('a'..'z' | 'A'..'Z') !(WS | '\n' | '\r')*;

terminal PATH:
	'/' ('/' | '.' | '_' | 'a'..'z' | 'A'..'Z' | '0'..'9')+;

terminal INT returns ecore::EDouble:
	'-'? ('0'..'9')+ ('.' ('0'..'9')+)?;

terminal STRING:
	'"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | '"'))* '"' |
	"'" ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | "'"))* "'";

terminal SL_COMMENT:
	'!--' !('\n' | '\r')* NEWLINE;

terminal WS:
	(' ' | '\t');

terminal NEWLINE:
	'\r'? '\n'?;

terminal ANY:
	.;