grammar org.lunifera.metamodel.dsl.behavior.stories.en.StoryDsl hidden(WS, NEWLINE, SL_COMMENT)
	

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.lunifera.org/metamodel/dsl/organization/en/OrganizationDsl" as org
import "http://www.lunifera.org/metamodel/dsl/software/composition/CompositeModelDsl" as composite
import "http://www.lunifera.org/metamodel/dsl/business/processes/BpModelDsl" as process
import "http://www.lunifera.org/metamodel/entity/Entity" as entity
generate storyDsl "http://www.lunifera.org/metamodel/dsl/behavior/stories/en/StoryDsl"

Story:
	description+=Description?
	meta=Meta?
	narrative=Narrative?
	scenarios+=Scenario*;


//ImportNamespace returns ImportNamespace :
//	'Import' importedNamespace=FqnWithWildCard
//;

Narrative:
	'Narrative:'
	((inOrderTo=InOrderTo?) &
	(asA=AsA?) &
	iWantTo=IWantTo);

Scenario:
	{Scenario}
	'Scenario:'
	name=Description
	(meta=Meta)?
	(givenStories=GivenStories)?
	(steps+=AbstractStep)*
	(examples=Examples)?;

GivenStories:
	'GivenStories:'
	resources+=StoryPath+;

StoryPath:
	path=PATH ','?;

Meta:
	{Meta}
	'Meta:'
	metaElements+=AbstractMetaElement (metaItems+=AbstractMetaElement)*;

AbstractMetaElement:
	
	(DefaultMetaElement | ProcessMetaElement | EntityMetaElement | ComponentMetaElement | StepClassMetaElement) 
;

DefaultMetaElement:
	'@' (name=ID value=ID)
;

ProcessMetaElement:
	'@' 'processMeta' process = [process::BusinessProcessDefinition|Fqn]
;

EntityMetaElement:
	'@' 'entityMeta' entity = [entity::LEntity|Fqn]
;

ComponentMetaElement:
	'@' 'unitMeta' component = [composite::UnitInstance|Fqn] 
;

StepClassMetaElement:
	'@' 'stepClassMeta' stepClass = Fqn 
;

AbstractStep:
	(GivenStep | WhenStep | ThenStep)
	(=> ands+=AndStep*)?;

GivenStep:
	{GivenStep}
	'Given' stepDescription=StepDescription NEWLINE*
	table=Table?;

WhenStep:
	{WhenStep}
	'When' stepDescription=StepDescription NEWLINE*
	table=Table?;

ThenStep:
	{ThenStep}
	'Then' stepDescription=StepDescription NEWLINE*
	table=Table?;

AndStep:
	{AndStep}
	'And' stepDescription=StepDescription NEWLINE*
	table=Table?;

InOrderTo:
	'In order to' name=NarrativeElementContent;

AsA:
	'As a' roles+=[org::BusinessRole|Fqn] (',' roles+=[org::BusinessRole|Fqn])*;

IWantTo:
	'I want to' name=NarrativeElementContent;

Examples:
	'Examples:'
	table=Table;

Table:
	rows+=TABLE_ROW+ NEWLINE*;

StepDescription:
	(pieces+=AbstractStepDescriptionPiece)+
;

AbstractStepDescriptionPiece :
	
	(ProcessSelection | EntitySelection | ComponentSelection | => NormalStepDescriptionPiece)
;

ProcessSelection:
	'process' process = [process::BusinessProcessDefinition|ID]
;

EntitySelection:
	'entity' entity = [entity::LEntity|ID]
;

ComponentSelection:
	'unit' unit = [composite::UnitInstance|ID] 
;

NormalStepDescriptionPiece:
	word = Word
;

Description returns ecore::EString hidden():
	Content NEWLINE+;
	

Word returns ecore::EString hidden():
	(WS | STRING | ID | INT | ANY_OTHER | PLACEHOLDER );

NarrativeElementContent returns ecore::EString:
	(STRING | ID | INT | ANY_OTHER | PLACEHOLDER) (STRING | ID | INT | ANY_OTHER | PLACEHOLDER)*;

Content returns ecore::EString:
	(WS | STRING | ID | INT | ANY_OTHER | PLACEHOLDER) (WS | STRING | ID | INT | ANY_OTHER | PLACEHOLDER)*;

//---


FqnWithWildCard:
	Fqn ('.' '*')?;

Fqn:
  ValidID (=>'.' ValidID)*;
  
ValidID:
	ID;
	
terminal ID:
	'^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;

terminal INT returns ecore::EDouble:
	'-'? ('0'..'9')+ ('.' ('0'..'9')+)?;

terminal PLACEHOLDER:
	'<' ('a'..'z' | 'A'..'Z') !('.' | '>' | '\n' | '\r')* '>';

terminal TABLE_ROW:
	'|' (!('|' | '\n' | '\r')* '|')+ WS* NEWLINE;

terminal PATH:
	'/' ('/' | '.' | '_' | 'a'..'z' | 'A'..'Z' | '0'..'9')+;

terminal STRING:
	'"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | '"'))* '"' |
	"'" ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | "'"))* "'";

terminal SL_COMMENT:
	'!--' !('\n' | '\r')* NEWLINE;

terminal WS:
	(' ' | '\t');

terminal NEWLINE:
	'\r'? '\n'?;

terminal ANY_OTHER:
	.;